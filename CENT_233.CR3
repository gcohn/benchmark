'Program name: CENT_233_V05.CR3
'Date written: 4-16-15 by AK

'Modifications:  
' 1. Version 2 created 2-15-2015. Added min/max air to 5 air temp probes (Air_150,
  '  Air_250, air_350, Asp_350, air_450. Added 5min max windspeed to prop anemometer.
  '  Commented out 60 and 15 minute tables. Remaining output 5min and daily. AK 
' 2. Version 3: created 4/16/2015: by AK
  '  Changed PROGID from FP2 to Long
  '  Changed TableFile name from 225 to 233
  '  Added 1.50m and 4.50m min/max dewpoint to 5 min output
  '  Removed VPD, VAP, and SatVP from output
' 3. Version 4 created 1/14/2016 by AK
  '  Removed daily output from program
  '  Changed TableFile("USB:CENT_233_Table440",8,-1,0,0,Hr,0,0) to TableFile("CRD:CENT_233_Table105_",64,-1,0,1,Day,0,0)
  '  Update OS to 28 and added NL116 with 4GB storage card.
'Version 5 Created 3/10/16 by GC
  '  Changes to CENT_234

Const SCAN_INTERVAL      = 1   'Scan interval in Sec for sonic
Const SCAN_INTERVAL_SLOW = 15  'Scan interval in Sec for rest of program
Const OUTPUT_INTERVAL    = 5   'Output interval in minutes
Const LID                = 233

'\\\\\\\\\\\\\\\\\\\\\\\\\ DECLARATIONS /////////////////////////

Public HMP_150(3) As Float
Public HMP_450(3) As Float
Public AIR_250
Public AIR_350
Public ASP_350
Public SOILT(4)
Public WCRWC(4)
Public WINDSPEED
Public WIND_DIR
Public BATTERY_V
Public WCRPA(4)
Public sncstring As String * 60
Public sncstring_bytes
Public snc(5)
Public stat_flag
Public Vh
Public diag_flag
Public nan_flag
Public strtosend
Public LOGGERID
Public PROGID As Long
Public AIR_150_diag=1 '1 = sensor installed; 0=sensor removed
Public AIR_250_diag=1 '1 = sensor installed; 0=sensor removed
Public PROG_VERS = 5

Alias HMP_150(1) = AIR_150
Alias HMP_150(2) = RH_150
Alias HMP_150(3) = DEWPT150
Alias HMP_450(1) = AIR_450
Alias HMP_450(2) = RH_450
Alias HMP_450(3) = DEWPT450
Alias SOILT(1)   = SOILT_10
Alias SOILT(2)   = SOILT_20
Alias SOILT(3)   = SOILT_50
Alias SOILT(4)   = SOILT_100
Alias WCRWC(1)   = WCRWC_10
Alias WCRWC(2)   = WCRWC_20
Alias WCRWC(3)   = WCRWC_50
Alias WCRWC(4)   = WCRWC_100
Alias WCRPA(1)   = WCRPA_10
Alias WCRPA(2)   = WCRPA_20
Alias WCRPA(3)   = WCRPA_50
Alias WCRPA(4)   = WCRPA_100
Alias snc(1)     = Ux         'positive for S--N
Alias snc(2)     = Uy         'positive for E--W
Alias snc(3)     = unit As String * 1
Alias snc(4)     = Ts         'in deg C
Alias snc(5)     = diag

Units AIR_150         = deg C
Units AIR_250         = deg C
Units AIR_350         = deg C
Units AIR_450         = deg C
Units ASP_350         = deg C
Units RH_150          = percent
Units RH_450          = percent
Units DEWPT150        = deg C
Units DEWPT450        = deg C
Units SOILT_10        = deg C
Units SOILT_20        = deg C
Units SOILT_50        = deg C
Units SOILT_100       = deg C
Units WCRWC_10        = fraction
Units WCRWC_20        = fraction
Units WCRWC_50        = fraction
Units WCRWC_100       = fraction
Units WCRPA_10        = number
Units WCRPA_20        = number
Units WCRPA_50        = number
Units WCRPA_100       = number
Units WINDSPEED       = m s-1
Units WIND_DIR        = degrees
Units BATTERY_V       = volts
Units Ux              = [m s-1]
Units Uy              = [m s-1]
Units Ts              = [deg C]
Units diag            = [1]
Units Vh              = [m s-1]
Units LOGGERID        = number
Units PROGID          = number
Units AIR_150_diag    = number
Units AIR_250_diag    = number

Dim n                 = 1

'SYSTEM Diagnostics
'#######################################################
'SYS Table variables


'COEF defenitions
Public coef_AIR_150 = 0.1
Public offset_AIR_150 = -40
Public coef_AIR_450 = 0.1
Public offset_AIR_450 = -40
Public coef_RH_150 = 0.1
Public coef_RH_450 = 0.1
Public coef_WCRPA = 0.001
Public coef_WINDSPEED = 0.098

DataTable(SYS,true,-1)
  TableFile("CRD:CENT_233_SYS_",64,-1,0,1,Day,0,0)
  DataInterval(0,5,min,10)
  Sample (1,LOGGERID,FP2)
  Sample (1,PROGID,Long)
  Sample (1,PROG_VERS,FP2)
  Sample(1,coef_AIR_150,FP2)
  Sample(1,coef_AIR_450,FP2)
  Sample(1,offset_AIR_150,FP2)
  Sample(1,offset_AIR_450,FP2)
  Sample(1,coef_RH_150,FP2)
  Sample(1,coef_RH_450,FP2)
  Sample(1,coef_WCRPA,FP2)
  Sample(1,coef_WINDSPEED,FP2)
  Sample(1,WCRPA_10,FP2)
  Sample(1,WCRPA_20,FP2)
  Sample(1,WCRPA_50,FP2)
  Sample(1,WCRPA_100,FP2)
EndTable

'\\\\\\\\\\\\\\\\\\\\\\\\ PROGRAM OUTPUT ////////////////////////
'5 MINUTE OUTPUT
DataTable(Table105,true,-1)
  TableFile("CRD:CENT_233_Table105_",64,-1,0,1,Day,0,0)
  DataInterval(0,5,Min,10)
  Sample(1,LOGGERID,FP2)
  Sample(1,PROGID,Long)
  Average(1,AIR_150,FP2,0)
  Average(1,AIR_250,FP2,0)
  Average(1,AIR_350,FP2,0)
  Average(1,AIR_450,FP2,0)
  Average(1,ASP_350,FP2,0)
  Average(1,RH_150,FP2,0)
  Average(1,RH_450,FP2,0)
  Average(1,DEWPT150,FP2,0)
  Average(1,DEWPT450,FP2,0)
  Average(4,SOILT,FP2,0)
  Average(4,WCRWC,FP2,0)
  Average(1,WINDSPEED,FP2,0)
  Sample(1,WIND_DIR,FP2)
  WindVector(1,WINDSPEED,WIND_DIR,FP2,False,0,0,0)
  Average(1,BATTERY_V,FP2,0)
  Sample(1,AIR_150_diag,FP2)
  Sample(1,AIR_250_diag,FP2)
  Totalize (1,n,FP2,False)  'START OF SNOIC OUTPUT
  FieldNames ("N_samples")
  Totalize (1,n,FP2,diag<>0)
  FieldNames ("N_samples_good")
  Maximum (1,diag,FP2,diag_flag,false)
  Minimum (1,sncstring_bytes,FP2,False,False)
  Average (2,snc(1),FP2,stat_flag)
  Average (1,snc(4),FP2,stat_flag)
  StdDev (2,snc(1),FP2,stat_flag)
  StdDev (1,snc(4),FP2,stat_flag)
  WindVector (1,Uy,-Ux,FP2,stat_flag,SCAN_INTERVAL*60*OUTPUT_INTERVAL/6,1,0)
  FieldNames ("SPD_mean,DIR_mean,DIR_std") ' field names for sonic data
  Maximum (1,Vh,FP2,stat_flag,False)
  FieldNames ("SPD_Gust_max")
  Maximum (1,AIR_150,FP2,False,False)
  Maximum (1,AIR_250,FP2,False,False)
  Maximum (1,AIR_350,FP2,False,False)
  Maximum (1,ASP_350,FP2,False,False)
  Maximum (1,AIR_450,FP2,False,False)
  Maximum(1,DEWPT150,FP2,False,False)
  Maximum(1,DEWPT450,FP2,False,False)
  Minimum (1,AIR_150,FP2,False,False)
  Minimum (1,AIR_250,FP2,False,False)
  Minimum (1,AIR_350,FP2,False,False)
  Minimum (1,ASP_350,FP2,False,False)
  Minimum (1,AIR_450,FP2,False,False)
  Minimum(1,DEWPT150,FP2,False,False)
  Minimum(1,DEWPT450,FP2,False,False)
  Maximum (1,WINDSPEED,FP2,False,False)
  
EndTable


'\\\\\\\\\\\\\\\\\\\\\\\\\\\ MAIN PROGRAM ////////////////////////////
'
'MEASURE GILL WINDOBSERVER II SONIC ANAMOMETER
BeginProg
  SW12 (1,0)
  Delay (1,5,Sec)
  SW12 (1,1)
  SerialOpen (Com1,9600,16,0,100)
  SerialFlush(Com1)
  Delay (1,5,Sec)
  nan_flag = 0
  Scan (SCAN_INTERVAL,Sec,0,0)
    SerialInRecord (Com1,sncstring,0,0,0,sncstring_bytes,01)
    Delay (1,10,mSec)
    SplitStr (snc(1),sncstring,",",5,4)
    SerialFlush (Com1)
    If diag = NaN Then
      diag_flag = 1
      stat_flag = 1
      nan_flag = nan_flag + 1
    ElseIf diag <> 0
      diag_flag = 0
      stat_flag = 1
    ElseIf Ux = NaN OR Uy = NaN OR Ts = NaN
      diag_flag = 0
      stat_flag = 1
      nan_flag = nan_flag + 1
    Else
      diag_flag = 0
      stat_flag = 0
      nan_flag = 0
    EndIf
    Vh  =  SQR(Ux^2+Uy^2)
    If nan_flag > 1 Then
      SW12 (1,0)
      Delay (1,5,Sec)
      SW12 (1,1)
      SerialClose (Com1)
      Delay (1,5,Sec)
      SerialOpen (Com1,9600,16,0,100)
      SerialFlush (Com1)
      nan_flag = 0
    EndIf
    '
    'MEASURE WIND SPEED
    PulseCount(WINDSPEED,1,1,1,1,coef_WINDSPEED,0)
  NextScan

  SlowSequence
  Scan(SCAN_INTERVAL_SLOW,Sec, 0, 0)
    '
    'MEASURE 1.5M & 4.5M HMP45C'S
    SW12 (2,1)
    Delay(0,150,mSec)
    VoltDiff(AIR_150,1,mv1000,1,0,0,_60Hz,coef_AIR_150,offset_AIR_150)
    VoltDiff(RH_150,1,mv1000,2,0,0,_60Hz,coef_RH_150,0)
    VoltDiff(AIR_450,1,mv1000,3,0,0,_60Hz,coef_AIR_450,offset_AIR_150)
    VoltDiff(RH_450,1,mv1000,4,0,0,_60Hz,coef_RH_450,0)
    SW12 (2,0)
    '
    'MEASURE 4 SOIL TEMPS
    Therm107(SOILT,4,9,VX1,0,_60Hz,1,0)
    '
    'MEASURE FOUR WATER CONTENT REFLECTOMETERS
    PortSet(4,1)
    PeriodAvg(WCRPA,4,mV1000,13,0,0,10,50,coef_WCRPA,0)
    PortSet(4,0)
    '
    'MEASURE WIND DIRECTION
    BrHalf(WIND_DIR,1,mv5000,17,VX2,1,5000,True,0,_60Hz,355,0)
    '
    'MEASURE 2.5M AIR TEMP
    Therm107(AIR_250,1,18,VX3,0,_60Hz,1,0)
    '
    'MEASURE 3.5M AIR TEMP
    Therm107(AIR_350,1,19,VX3,0,_60Hz,1,0)
    '
    'MEASURE 3.5M ASPIRATED AIR TEMP
    Therm107(ASP_350,1,20,VX3,0,_60Hz,1,0)
    '
    'MEASURE BATTERY VOLTAGE
    Battery(BATTERY_V)
    '
    'PART2: PROCESS DATA
    '
    'SET RH TO 100% IF GREATER THAN 100%
    If RH_150>100 AND RH_150<108 Then RH_150=100
    If RH_450>100 AND RH_450<108 Then RH_450=100
    '
    'COMPUTE 1.5M & 4.5M DEWPOINT
    DewPoint(DEWPT150,AIR_150,RH_150)
    DewPoint(DEWPT450,AIR_450,RH_450)
    '
    'CONVERT WCR PERIOD TO WATER CONTENT    
    WCRWC_10=-0.187+0.037*WCRPA_10+0.335*WCRPA_10^2+0*WCRPA_10^3+0*WCRPA_10^4+0*WCRPA_10^5
    WCRWC_20=-0.187+0.037*WCRPA_20+0.335*WCRPA_20^2+0*WCRPA_20^3+0*WCRPA_20^4+0*WCRPA_20^5
    WCRWC_50=-0.187+0.037*WCRPA_50+0.335*WCRPA_50^2+0*WCRPA_50^3+0*WCRPA_50^4+0*WCRPA_50^5
    WCRWC_100=-0.187+0.037*WCRPA_100+0.335*WCRPA_100^2+0*WCRPA_100^3+0*WCRPA_100^4+0*WCRPA_100^5
    '
    'CORRECT WIND DIRECTION MEASUREMENT
    If WIND_DIR>360 Then WIND_DIR=0
    If WIND_DIR<0 Then WIND_DIR=0
    '
    'PART 3: OUTPUT DATA
    '
    'SET LOGGERID TO
    LOGGERID = LID
    '
    'RECORD THE SIGNATURE AS PROGID
    If  TimeIntoInterval(0,1,Min) Then
      PROGID=Status.ProgSignature(1,1)
    EndIf
    '
    '5 MINUTE OUTPUT
    CallTable Table105
    CallTable SYS
  NextScan
EndProg
