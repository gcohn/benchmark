;{CR10X}
;PROGRAM: HI-15.DLD  HI-15 Met. Station
;
;1. Modified 1-15-96 changed to edlog format.
;
;2. Modified 8-28-96 changed *A2 from 64 to 128.
;
;3. Modified 3-27-97
;     A. Added total tipping bucket counts, *6 19
;     B. Configured control port 2 to 100ms
;     C. Set array IDs
;
;4. Modified on 8-22-02
;     Replaced two HMP35A relative humidity instruments with two HMP45AC instruments.
;     Also added a Tower Junction Box, solar panel, charge controller and two 6V
;     golf cart batteries.
;
;5. Modified on 10-20-05
;     Calibrated shelter raingage with Craig, then changed multiplier on raingage transducer
;     instruction from 12.555 to 12.409.
;
;6. Verison 06 modified 9-4-12.  Added LoggerID and ProgramID to each output array.  Changed
;   array ID's - Daily 135 to 440, 60 min 131 to 160, 5 min 126 to 105.  Changed real time
;   output from 120 (day,hour/min) to 1220 (year,day,hour/min).  Added 15 minute output array
;   (115) for average air temps.
;
;7. Version 07 modified 10-25-12.  Removed LoggerID and Program Id on each output array.
;   It was too much data for the SM192 storage module to handle.
;
;8. Version 08 modified 12-12-12.  Removed tower sensors from datalogger.  Will add 2nd
;   datalogger on tower to measure 1.5M and 4.5M HMP45C's plus wind monitor.
;
;9. Version 09 modified 5-21-15.  Removed A-35 and installed Stevens PAT.
;
*Table 1 Program
  01: 10        Execution Interval (seconds)
;
;PART 1: MEASURE SENSORS
;
;MEASURE RAINGAGE TRANSDUCER

1:  Full Bridge (P6)
 1: 1        Reps
 2: 2        7.5 mV Slow Range
 3: 5        DIFF Channel
 4: 3        Excite all reps w/Exchan 3
 5: 2500     mV Excitation
 6: 11       Loc [ PRECIP_PR ]
 7: 12.409   Mult
 8: 0        Offset
;
;COUNT TIPPING BUCKET

2:  Pulse (P3)
 1: 1        Reps
 2: 2        Pulse Input Channel
 3: 2        Switch Closure, All Counts
 4: 12       Loc [ TIPPING_B ]
 5: 1        Mult
 6: 0        Offset
;
;MEASURE STEVENS PAT

3:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 1        SE Channel
 4: 1        Excite all reps w/Exchan 1
 5: 10       Delay (0.01 sec units)
 6: 2500     mV Excitation
 7: 31       Loc [ PAT       ]
 8: 1.0      Multiplier
 9: 0.0      Offset
;
;MEASURE BATTERY VOLTAGE

4:  Batt Voltage (P10)
 1: 28       Loc [ BATTERY_V ]
;
;PART 2: PROCESS DATA
;
;COMPUTE TOTAL TIPS

5:  Z=X+Y (P33)
 1: 12       X Loc [ TIPPING_B ]
 2: 18       Y Loc [ TOTAL_TIP ]
 3: 18       Z Loc [ TOTAL_TIP ]
;
;CHANGE TIPS TO INCHS

6:  Z=X*F (P37)
 1: 12       X Loc [ TIPPING_B ]
 2: .01      F
 3: 12       Z Loc [ TIPPING_B ]
;
;SET LOGGERID TO 207

7:  Z=F x 10^n (P30)
 1: 207      F
 2: 0        n, Exponent of 10
 3: 29       Z Loc [ LOGGERID  ]
;
;RECORD SIGNATURE AS PROGID

8:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 30       Then Do

     9:  Signature (P19)
      1: 30       Loc [ PROGID    ]

10:  End (P95)
;
;5 MINUTE OUTPUT

11:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 5        Interval (same units as above)
 3: 10       Set Output Flag High

12:  Set Active Storage Area (P80)^19954
 1: 1        Final Storage Area 1
 2: 105      Array ID or Loc [ _________ ]

13:  Sample (P70)^4518
 1: 1        Reps
 2: 29       Loc [ LOGGERID  ]

14:  Sample (P70)^7424
 1: 1        Reps
 2: 30       Loc [ PROGID    ]

15:  Real Time (P77)^23701
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

16:  Resolution (P78)
 1: 1        High Resolution

17:  Sample (P70)^21791
 1: 1        Reps
 2: 11       Loc [ PRECIP_PR ]

18:  Totalize (P72)^13864
 1: 1        Reps
 2: 12       Loc [ TIPPING_B ]

19:  Sample (P70)^11433
 1: 1        Reps
 2: 31       Loc [ PAT       ]
;
;DAILY OUTPUT

20:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High

21:  Set Active Storage Area (P80)^29091
 1: 1        Final Storage Area 1
 2: 440      Array ID or Loc [ _________ ]

22:  Sample (P70)^7969
 1: 1        Reps
 2: 29       Loc [ LOGGERID  ]

23:  Sample (P70)^16487
 1: 1        Reps
 2: 30       Loc [ PROGID    ]

24:  Real Time (P77)^6480
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

25:  Totalize (P72)^5135
 1: 1        Reps
 2: 12       Loc [ TIPPING_B ]

26:  Serial Out (P96)
 1: 71       SM192/SM716/CSM1

*Table 2 Program
  02: 0         Execution Interval (seconds)

*Table 3 Subroutines

End Program










-Input Locations-
1 _________ 1 0 0
2 _________ 1 0 0
3 _________ 1 0 0
4 _________ 1 0 0
5 _________ 1 0 0
6 _________ 1 0 0
7 _________ 0 0 0
8 _________ 0 0 0
9 _________ 0 0 0
10 _________ 0 0 0
11 PRECIP_PR 1 0 1
12 TIPPING_B 1 0 1
13 _________ 0 0 0
14 _________ 0 0 0
15 _________ 0 0 0
16 _________ 0 0 0
17 _________ 0 0 0
18 TOTAL_TIP 0 0 0
19 _________ 0 0 0
20 _________ 0 0 0
21 _________ 0 0 0
22 _________ 0 0 0
23 _________ 0 0 0
24 _________ 0 0 0
25 _________ 0 0 0
26 _________ 0 0 0
27 _________ 0 0 0
28 BATTERY_V 1 0 1
29 LOGGERID  1 2 1
30 PROGID    1 2 1
31 PAT       1 1 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X Compile Setting-
3
-CR10X RS-232 Setting-
-1
-DLD File Labels-
0
-Final Storage Labels-
0,105,19954
1,Year_RTM,23701
1,Day_RTM
1,Hour_Minute_RTM
2,PRECIP_PR~11,21791
3,TIPPING_B_TOT~12,13864
4,440,29091
5,Year_RTM,6480
5,Day_RTM
5,Hour_Minute_RTM
6,TIPPING_B_TOT~12,5135
7,LOGGERID~29,4518
8,PROGID~30,7424
9,LOGGERID~29,7969
10,PROGID~30,16487
11,PAT~31,11433
