'CR1000 Series Datalogger
'Program name: H15MET_211_V01.0.CR1
'Program for HI15 rain gauge and associated sensors
'
'Version 1.0 Created on 4/13/2017 FOR UPGRADE from CR10X pair to single CR1000
    ' Removed RH, Dewpoint, VPD calculations and subroutines from program.
    ' Removed 15min, 60min and daily summary tables from output.
    ' Added 107 calls, panel temp, _Check variables.
    ' Modified diff, SE, P1, and VX channels in program calls for CRBasic
    ' Added TableName.Output to end of program to replace TimeIntoInterval to write _check values to maint table. 

'PROGRAM VARIABLES
'////////////////////////////////////////////////////         
'Declare Constant Variable
Const LID              = 211

'Call PreserveVariables so variables will reflect last known value if the data logger experiences a power loss.
PreserveVariables

'Declare Public Variables
Public LOGGERID
Public PROGID As Long
Public PROG_VERS=1.0
Public REF_TEMP
Public BATTERY_V_CHECK = NAN
Public BATTERY_V

Public SH_PRECIP_01
Public SH_PRECIP_02

Public AIR_TEMP_CHECK = NAN
Public AIR_TEMP(2)

Public TIPPING_B
Public TOTAL_TIPS


'Alias
Alias AIR_TEMP(1)  = SH_TEMP
Alias AIR_TEMP(2)  = AIR_450


'Declare Public Units
Units AIR_TEMP        = deg C
Units REF_TEMP        = deg C
Units SH_PRECIP_01     = millimeter
Units SH_PRECIP_02     = millimeter
Units TIPPING_B       = tips
Units LOGGERID        = number
Units PROGID          = number
Units BATTERY_V       = volts

'SYSTEM Diagnostics
'#######################################################
'SYS Table variables
'COEF defenitions
'Pressure Transducer- Apr 2017
Public coef_SH_PRECIP_01 = 12.409*25.4
Public offset_SH_PRECIP_01 = 0
'PAT- Apr 2017
'convert from feet to inches to mm (*12*25.4)
Public coef_SH_PRECIP_02 = 0.001*12*25.4
Public offset_SH_PRECIP_02 = 0
'convert from tips to inches 0.01
Public coef_TIPPING_B = 1

DataTable(SYS,true,-1)
  TableFile("USB:H15MET_211_SYS_",8,-1,0,0,Hr,0,0)
  DataInterval(0,5,min,10)
  Sample (1,LOGGERID,FP2)
  Sample (1,PROGID,Long)
  Sample (1,PROG_VERS,FP2)
  Minimum(1,BATTERY_V,FP2,False,False)
  Sample(1,coef_SH_PRECIP_01,FP2)
  Sample(1,offset_SH_PRECIP_01,FP2)  
  Sample(1,coef_SH_PRECIP_02,FP2)
  Sample(1,offset_SH_PRECIP_02,FP2)
EndTable

'\\\\\\\\\\\\\\\\\\\\\\\\PROGRAM OUTPUT////////////////////////
'5 MINUTE OUTPUT
DataTable(Table105,true,-1)
  DataInterval(0,5,Min,10)
  TableFile("USB:H15MET_211_Table105_",8,-1,0,0,Hr,0,0)
  Sample(1, LOGGERID, FP2)
  Sample(1,PROGID,Long)
  Sample(1,AIR_TEMP_Check,FP2)
  Average(2,AIR_TEMP,FP2,False)
  Maximum(2,AIR_TEMP,FP2,False,0)
  Minimum(2,AIR_TEMP,FP2,False,0)
  Sample(1,BATTERY_V_Check,FP2)
  Average(1,BATTERY_V,FP2,False)
  Sample(1,SH_PRECIP_01,FP2)
  Sample(1,SH_PRECIP_02,FP2)
  Totalize(1,TIPPING_B,FP2,0)
EndTable

'\\\\\\\\\\\\\\\\\\\\\\\\\\\ PROGRAM ////////////////////////////

BeginProg
  BATTERY_V_Check = NAN 'initialize Nan
  AIR_TEMP_CHECK = NAN  'initialize Nan
  Scan (10,Sec,1,0)
    'MEASURE BATTERY VOLTAGE
    Battery(BATTERY_V)
    '
    'MEASURE REFERENCE TEMPERATURE
    PanelTemp(REF_TEMP,_60Hz)
    '
    'MEASURE AIR TEMPERATURES (THERMISTORS)
    Therm107(AIR_TEMP,2,1,1,0,_60Hz,1,0)
    '
    'MEASURE RAINGAGE TRANSDUCER
    BrFull(SH_PRECIP_01, 1, mV7_5,2,2, 1, 2500, true, true, 0, 250,coef_SH_PRECIP_01, offset_SH_PRECIP_01)
    '
    'MEASURE STEVENS PAT
    BrHalf(SH_PRECIP_02, 1, mV2500,5,3, 1, 2500, False,50000, 250,coef_SH_PRECIP_02, offset_SH_PRECIP_02)
    '
    'COUNT TIPPING BUCKET
    PulseCount(TIPPING_B, 1,1, 2, 0, coef_TIPPING_B, 0)
    

    'PROCESS DATA
    '
    'COMPUTE TOTAL TIPS
    TOTAL_TIPS = TIPPING_B + TOTAL_TIPS

    'OUTPUT DATA
    'SET LOGGERID TO LID defined at top
    LOGGERID = LID
    'RECORD SIGNATURE AS PROGID
    If  TimeIntoInterval(0,1,Min) Then
      PROGID=Status.ProgSignature(1,1)
    EndIf

    If (Table105.Output(1,1)) Then
      BATTERY_V_CHECK = NAN
      AIR_TEMP_CHECK = NAN
    EndIf
    
   '5 MINUTE OUTPUT
    CallTable Table105
    CallTable SYS
  NextScan
EndProg

