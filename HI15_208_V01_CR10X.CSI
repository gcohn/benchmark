;{CR10X}
;PROGRAM: HI15_208_V01_CR10X.CSI.  Created on 1-30-13 by JM.  Datalogger on HI15 tower.
;
*Table 1 Program
  01: 10        Execution Interval (seconds)
;
;PART 1: MEASURE SENSORS
;
;TURN ON BOTH HMP45C'S

1:  Do (P86)
 1: 45       Set Port 5 High
;
;PAUSE FOR 150 mSEC FOR INSTRUMENTS TO STABILIZE

2:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 15       Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation
;
;MEASURE 1.5M AND 4.5M AIR TEMPERATURES

3:  Volt (Diff) (P2)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 1        DIFF Channel
 4: 3        Loc [ AIR_150   ]
 5: 0.1      Mult
 6: -40      Offset
;
;MEASURE 1.5M AND 4.5M RELATIVE HUMIDITYS

4:  Volt (Diff) (P2)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 3        DIFF Channel
 4: 5        Loc [ RH_150    ]
 5: 0.1      Mult
 6: 0        Offset
;
;TURN OFF BOTH HMP45AC'S

5:  Do (P86)
 1: 55       Set Port 5 Low
;
;MEASURE WIND DIRECTION

6:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 11       SE Channel
 4: 1        Excite all reps w/Exchan 1
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 1        Loc [ WIND_DIR  ]
 8: .142     Mult
 9: 0        Offset
;
;MEASURE WIND SPEED

7:  Pulse (P3)
 1: 1        Reps
 2: 1        Pulse Input Channel
 3: 11       Low Level AC, Counts (Discard Bad Intervals)
 4: 2        Loc [ WINDSPEED ]
 5: .0098    Mult
 6: 0        Offset
;
;MEASURE BATTERY VOLTAGE

8:  Batt Voltage (P10)
 1: 28       Loc [ BATTERY_V ]
;
;PART 2: PROCESS DATA
;
;SET RH TO 100 IF GREATER THAN 100

9:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count

     10:  If (X<=>F) (P89)
      1: 5     -- X Loc [ RH_150    ]
      2: 3        >=
      3: 100      F
      4: 30       Then Do

          11:  Z=F (P30)
           1: 100      F
           2: 0        Exponent of 10
           3: 5     -- Z Loc [ RH_150    ]

     12:  End (P95)

13:  End (P95)
;
;COMPUTE DEWPOINT TEMPERATURE AT 1.5M

14:  Z=X (P31)
 1: 3        X Loc [ AIR_150   ]
 2: 13       Z Loc [ TEMP1     ]

15:  Z=X (P31)
 1: 5        X Loc [ RH_150    ]
 2: 14       Z Loc [ TEMP2     ]

16:  Do (P86)
 1: 1        Call Subroutine 1

17:  Z=X (P31)
 1: 8        X Loc [ DEWPT_450 ]
 2: 7        Z Loc [ DEWPT_150 ]
;
;COMPUTE VAPOR PRESSURE DEFICIT AT 1.5M

18:  Do (P86)
 1: 2        Call Subroutine 2

19:  Z=X (P31)
 1: 10       X Loc [ VAPDEF450 ]
 2: 9        Z Loc [ VAPDEF150 ]
;
;COMPUTE DEWPOINT TEMPERATURE AT 4.5M

20:  Z=X (P31)
 1: 4        X Loc [ AIR_450   ]
 2: 13       Z Loc [ TEMP1     ]

21:  Z=X (P31)
 1: 6        X Loc [ RH_450    ]
 2: 14       Z Loc [ TEMP2     ]

22:  Do (P86)
 1: 1        Call Subroutine 1
;
;COMPUTE VAPOR PRESSURE DEFICIT AT 4.5M

23:  Do (P86)
 1: 2        Call Subroutine 2
;
;PART 3: OUTPUT DATA
;
;SET LOGGERID TO 208

24:  Z=F x 10^n (P30)
 1: 208      F
 2: 0        n, Exponent of 10
 3: 29       Z Loc [ LOGGERID  ]
;
;RECORD SIGNATURE AS PROGID

25:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 30       Then Do

     26:  Signature (P19)
      1: 30       Loc [ PROGID    ]

27:  End (P95)
;
;15 MINUTE OUTPUT

28:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 15       Interval (same units as above)
 3: 10       Set Output Flag High

29:  Set Active Storage Area (P80)^2795
 1: 1        Final Storage Area 1
 2: 115      Array ID

30:  Sample (P70)^21386
 1: 1        Reps
 2: 29       Loc [ LOGGERID  ]

31:  Sample (P70)^15010
 1: 1        Reps
 2: 30       Loc [ PROGID    ]

32:  Real Time (P77)^31954
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

33:  Average (P71)^25215
 1: 8        Reps
 2: 3        Loc [ AIR_150   ]

34:  Wind Vector (P69)^16761
 1: 1        Reps
 2: 30       Samples per Sub-Interval
 3: 2        S, U, theta(u), sigma(theta(u)) with polar sensor
 4: 2        Wind Speed/East Loc [ WINDSPEED ]
 5: 1        Wind Direction/North Loc [ WIND_DIR  ]
;
;60 MINUTE OUTPUT

35:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High

36:  Set Active Storage Area (P80)^1672
 1: 1        Final Storage Area 1
 2: 160      Array ID or Loc [ _________ ]

37:  Sample (P70)^12610
 1: 1        Reps
 2: 29       Loc [ LOGGERID  ]

38:  Sample (P70)^18402
 1: 1        Reps
 2: 30       Loc [ PROGID    ]

39:  Real Time (P77)^6517
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

40:  Wind Vector (P69)^670
 1: 1        Reps
 2: 30       Samples per Sub-Interval
 3: 2        S, U, éu, å(éu) Polar
 4: 2        Wind Speed/East Loc [ WINDSPEED ]
 5: 1        Wind Direction/North Loc [ WIND_DIR  ]

41:  Average (P71)^20357
 1: 8        Reps
 2: 3        Loc [ AIR_150   ]
;
;DAILY OUTPUT

42:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High

43:  Set Active Storage Area (P80)^27501
 1: 1        Final Storage Area 1
 2: 440      Array ID or Loc [ _________ ]

44:  Sample (P70)^7969
 1: 1        Reps
 2: 29       Loc [ LOGGERID  ]

45:  Sample (P70)^16487
 1: 1        Reps
 2: 30       Loc [ PROGID    ]

46:  Real Time (P77)^6480
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

47:  Wind Vector (P69)^1156
 1: 1        Reps
 2: 30       Samples per Sub-Interval
 3: 2        S, U, éu, å(éu) Polar
 4: 2        Wind Speed/East Loc [ WINDSPEED ]
 5: 1        Wind Direction/North Loc [ WIND_DIR  ]

48:  Histogram (P75)^31903
 1: 1        Reps
 2: 8        No. of Bins
 3: 1        Closed Form
 4: 1        Bin Select Value Loc [ WIND_DIR  ]
 5: 2        WV Loc Option [ WINDSPEED ]
 6: 0        Low Limit
 7: 360      High Limit

49:  Average (P71)^10440
 1: 8        Reps
 2: 3        Loc [ AIR_150   ]

50:  Maximize (P73)^1905
 1: 9        Reps
 2: 10       Value with Hr-Min
 3: 2        Loc [ WINDSPEED ]

51:  Minimize (P74)^26865
 1: 8        Reps
 2: 10       Value with Hr-Min
 3: 3        Loc [ AIR_150   ]

52:  Serial Out (P96)
 1: 71       SM192/SM716/CSM1

*Table 2 Program
  02: 0         Execution Interval (seconds)

*Table 3 Subroutines

1:  Beginning of Subroutine (P85)
 1: 1        Subroutine 1

     2:  Z=X*F (P37)
      1: 13       X Loc [ TEMP1     ]
      2: 17.269   F
      3: 8        Z Loc [ DEWPT_450 ]

     3:  Z=X+F (P34)
      1: 13       X Loc [ TEMP1     ]
      2: 237.3    F
      3: 17       Z Loc [ TEMP5     ]

     4:  Z=X/Y (P38)
      1: 8        X Loc [ DEWPT_450 ]
      2: 17       Y Loc [ TEMP5     ]
      3: 8        Z Loc [ DEWPT_450 ]

     5:  Z=EXP(X) (P41)
      1: 8        X Loc [ DEWPT_450 ]
      2: 15       Z Loc [ TEMP3     ]

     6:  Z=X*F (P37)
      1: 15       X Loc [ TEMP3     ]
      2: 6.1078   F
      3: 15       Z Loc [ TEMP3     ]

     7:  Z=X*F (P37)
      1: 14       X Loc [ TEMP2     ]
      2: .01      F
      3: 17       Z Loc [ TEMP5     ]

     8:  Z=LN(X) (P40)
      1: 17       X Loc [ TEMP5     ]
      2: 17       Z Loc [ TEMP5     ]

     9:  Z=X+Y (P33)
      1: 17       X Loc [ TEMP5     ]
      2: 8        Y Loc [ DEWPT_450 ]
      3: 17       Z Loc [ TEMP5     ]

     10:  Z=X*F (P37)
      1: 17       X Loc [ TEMP5     ]
      2: 237.3    F
      3: 8        Z Loc [ DEWPT_450 ]

     11:  Z=X*F (P37)
      1: 17       X Loc [ TEMP5     ]
      2: -1.0     F
      3: 17       Z Loc [ TEMP5     ]

     12:  Z=X+F (P34)
      1: 17       X Loc [ TEMP5     ]
      2: 17.269   F
      3: 17       Z Loc [ TEMP5     ]

     13:  Z=X/Y (P38)
      1: 8        X Loc [ DEWPT_450 ]
      2: 17       Y Loc [ TEMP5     ]
      3: 8        Z Loc [ DEWPT_450 ]

14:  End (P95)

15:  Beginning of Subroutine (P85)
 1: 2        Subroutine 2

     16:  Z=X*Y (P36)
      1: 14       X Loc [ TEMP2     ]
      2: 15       Y Loc [ TEMP3     ]
      3: 16       Z Loc [ TEMP4     ]

     17:  Z=X*F (P37)
      1: 16       X Loc [ TEMP4     ]
      2: .01      F
      3: 16       Z Loc [ TEMP4     ]

     18:  Z=X-Y (P35)
      1: 15       X Loc [ TEMP3     ]
      2: 16       Y Loc [ TEMP4     ]
      3: 10       Z Loc [ VAPDEF450 ]

19:  End (P95)

End Program










-Input Locations-
1 WIND_DIR  1 1 1
2 WINDSPEED 1 1 1
3 AIR_150   5 1 1
4 AIR_450   17 1 1
5 RH_150    1 1 1
6 RH_450    1 0 0
7 DEWPT_150 0 0 0
8 DEWPT_450 0 0 0
9 VAPDEF150 0 0 0
10 VAPDEF450 0 0 0
11 _________ 1 0 0
12 _________ 1 0 0
13 TEMP1     0 0 0
14 TEMP2     0 0 0
15 TEMP3     0 0 0
16 TEMP4     0 0 0
17 TEMP5     0 0 0
18 _________ 0 0 0
19 _________ 0 0 0
20 _________ 0 0 0
21 _________ 0 0 0
22 _________ 0 0 0
23 _________ 0 0 0
24 _________ 0 0 0
25 _________ 0 0 0
26 _________ 0 0 0
27 _________ 0 0 0
28 BATTERY_V 1 0 1
29 LOGGERID  1 3 1
30 PROGID    1 3 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X Compile Setting-
3
-CR10X RS-232 Setting-
-1
-DLD File Labels-
0
-Final Storage Labels-
0,160,1672
1,WINDSPEED_S_WVT~2,670
1,WINDSPEED_U_WVT~2
1,WIND_DIR_DU_WVT~1
1,WIND_DIR_SDU_WVT~1
2,AIR_150_AVG~3,20357
2,AIR_450_AVG~4
2,RH_150_AVG~5
2,RH_450_AVG~6
2,DEWPT_150_AVG~7
2,DEWPT_450_AVG~8
2,VAPDEF150_AVG~9
2,VAPDEF450_AVG~10
3,440,27501
4,Year_RTM,6480
4,Day_RTM
4,Hour_Minute_RTM
5,WINDSPEED_S_WVT~2,1156
5,WINDSPEED_U_WVT~2
5,WIND_DIR_DU_WVT~1
5,WIND_DIR_SDU_WVT~1
6,WIND_DIR_BIN01~1,31903
6,WIND_DIR_BIN02~1
6,WIND_DIR_BIN03~1
6,WIND_DIR_BIN04~1
6,WIND_DIR_BIN05~1
6,WIND_DIR_BIN06~1
6,WIND_DIR_BIN07~1
6,WIND_DIR_BIN08~1
7,AIR_150_AVG~3,10440
7,AIR_450_AVG~4
7,RH_150_AVG~5
7,RH_450_AVG~6
7,DEWPT_150_AVG~7
7,DEWPT_450_AVG~8
7,VAPDEF150_AVG~9
7,VAPDEF450_AVG~10
8,WINDSPEED_MAX~2,1905
8,WINDSPEED_Hr_Min_MAX~2
8,AIR_150_MAX~3
8,AIR_150_Hr_Min_MAX~3
8,AIR_450_MAX~4
8,AIR_450_Hr_Min_MAX~4
8,RH_150_MAX~5
8,RH_150_Hr_Min_MAX~5
8,RH_450_MAX~6
8,RH_450_Hr_Min_MAX~6
8,DEWPT_150_MAX~7
8,DEWPT_150_Hr_Min_MAX~7
8,DEWPT_450_MAX~8
8,DEWPT_450_Hr_Min_MAX~8
8,VAPDEF150_MAX~9
8,VAPDEF150_Hr_Min_MAX~9
8,VAPDEF450_MAX~10
8,VAPDEF450_Hr_Min_MAX~10
9,AIR_150_MIN~3,26865
9,AIR_150_Hr_Min_MIN~3
9,AIR_450_MIN~4
9,AIR_450_Hr_Min_MIN~4
9,RH_150_MIN~5
9,RH_150_Hr_Min_MIN~5
9,RH_450_MIN~6
9,RH_450_Hr_Min_MIN~6
9,DEWPT_150_MIN~7
9,DEWPT_150_Hr_Min_MIN~7
9,DEWPT_450_MIN~8
9,DEWPT_450_Hr_Min_MIN~8
9,VAPDEF150_MIN~9
9,VAPDEF150_Hr_Min_MIN~9
9,VAPDEF450_MIN~10
9,VAPDEF450_Hr_Min_MIN~10
10,115,2795
11,LOGGERID~29,21386
12,PROGID~30,15010
13,AIR_150_AVG~3,25215
13,AIR_450_AVG~4
13,RH_150_AVG~5
13,RH_450_AVG~6
13,DEWPT_150_AVG~7
13,DEWPT_450_AVG~8
13,VAPDEF150_AVG~9
13,VAPDEF450_AVG~10
14,LOGGERID~29,12610
15,PROGID~30,18402
16,LOGGERID~29,7969
17,PROGID~30,16487
18,WINDSPEED_S_WVT~2,16761
18,WINDSPEED_U_WVT~2
18,WIND_DIR_DU_WVT~1
18,WIND_DIR_SDU_WVT~1
19,Year_RTM,31954
19,Day_RTM
19,Hour_Minute_RTM
20,Year_RTM,6517
20,Day_RTM
20,Hour_Minute_RTM
